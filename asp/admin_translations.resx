<%

user.checkifadmin

dim form : set form=aspl.form

dim addnew : set addnew=form.field("button")
addnew.add "html",l("addnew")
addnew.add "onclick","modalAspForm('admin_labeledit','','');return false;"
addnew.add "class","btn btn-primary"

dim table : table="<table id=""labels"" class=""table table-striped"">"
table=table & "<thead>"
table=table & "<tr>"
table=table & "<th>Code</th>"
table=table & "<th>"&l("language")&"</th>"
table=table & "<th>"&l("value")&"</th>"
table=table & "</tr>"
table=table & "</thead>"
table=table & "<tbody>"

dim language, languagelist : set languagelist=new cls_languageList

set languagelist=languagelist.list

dim rs : set rs=db.execute("select * from tblLabel order by sCode")

while not rs.eof
	table=table & "<tr>"
		table=table & "<td><a class=""link link-primary"" href=""#"" onclick=""modalAspForm('admin_labeledit'," & rs("iId") & ",'');return false;"">" & aspl.htmlEncode(rs("sCode")) & "</a></td>"
		table=table & "<td>" & languagelist(aspl.convertNmbr(rs("iLanguageID"))).sName & "</td>"
		table=table & "<td>" & threedots(aspl.htmlEncode(rs("sValue")),90) & "</td>"
	table=table & "</tr>"
	rs.movenext
wend

set rs=nothing

table=table & "</tbody></table>"

form.write table

form.writejs "drawSimpleDT('labels');"

form.build

%>